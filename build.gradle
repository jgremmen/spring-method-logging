plugins {
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
  id 'signing'
}


description 'Method Logging Extension for Spring'
group 'de.sayayi.lib'
version '0.4.1'


layout.buildDirectory = '.build'


ext {
  jetbrainsAnnotationsVersion = '24.0.1'
  junitVersion = '5.10.+'
  log4jVersion = '[2.20.0,3.0.0)'
  lombokVersion = '1.18.+'
  messageFormatVersion = '[0.8.0,)'
  slf4jVersion = '[2.0.0,2.1.0)'
  springFrameworkVersion = '[5.3.0,6.0.0)'
}


java {
  registerFeature('optional') {
    usingSourceSet sourceSets.main
  }

  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}


compileTestJava {
  options.debugOptions.debugLevel = 'lines,vars,source'
  options.compilerArgs = [ '-parameters' ]
}


test {
  useJUnitPlatform()
}


apply from: file('gradle/javadoc.gradle')
apply from: file('gradle/quality.gradle')
apply from: file('gradle/publishing.gradle')


repositories {
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots/'
    mavenContent {
      includeGroup 'de.sayayi.lib'
      snapshotsOnly()
    }
  }

  mavenCentral()
}


dependencies {
  // java
  compileOnly "org.jetbrains:annotations-java5:${jetbrainsAnnotationsVersion}"

  optionalCompileOnlyApi "org.slf4j:slf4j-api:${slf4jVersion}"
  optionalCompileOnlyApi "org.apache.logging.log4j:log4j-api:${log4jVersion}"

  implementation "org.springframework:spring-context:${springFrameworkVersion}"
  implementation "de.sayayi.lib:message-format:${messageFormatVersion}"


  // test
  testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

  testCompileOnly "org.jetbrains:annotations-java5:${jetbrainsAnnotationsVersion}"
  testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testImplementation "org.springframework:spring-test:${springFrameworkVersion}"

  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}
